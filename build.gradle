plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}

group 'com.froobworld'
version '1.0.3-FOX'
jar.enabled = false;

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url 'https://repo.essentialsx.net/releases/'
    }
    maven {
        url = uri("https://maven.pkg.github.com/mcfoxcraft/nab-configuration")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    compileOnly 'dev.folia:folia-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly 'net.essentialsx:EssentialsX:2.19.7'
    implementation 'com.froobworld:nab-configuration:1.0.2'
    implementation 'org.bstats:bstats-bukkit:3.0.1'
}

processResources {
    filter { String line -> line.replace('${version}', project.version.toString()) }
}

shadowJar {
    destinationDirectory.set(file('out'))
    archiveFileName = 'SeeMore-' + project.version.toString() + '.jar';

    relocate 'com.froobworld.nabconfiguration', 'com.froobworld.seemore.lib.nabconfiguration'
    relocate 'org.bstats','com.froobworld.seemore.lib.bstats'
}

tasks.register('copyToRunServer', Copy) {
    from layout.projectDirectory.dir('out')
    include '*.jar'
    into file('run/plugins')
}

tasks.named("build").get().dependsOn(shadowJar)
tasks.named("build").get().finalizedBy(tasks.named("copyToRunServer"))
